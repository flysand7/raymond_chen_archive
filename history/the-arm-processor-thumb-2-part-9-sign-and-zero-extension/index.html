<body style=max-width:80ch;margin:auto>
<h1 class="entry-title" style="margin-bottom:15px;">The ARM processor (Thumb-2), part 9: Sign and zero extension</h1>  <!-- .entry-meta -->

<p>I noted <a href="/history/the-arm-processor-thumb-2-part-8-bit-shifting-and-bitfield-access" title="The ARM processor (Thumb-2), part 8: Bit shifting and bitfield access"> last time</a> that you could use the bitfield extraction instructions to do zero- and sign-extension of bytes and halfwords to words. But there are dedicated instructions for these operations which have smaller encodings if the source and destination registers are low.</p>
<pre>    ; unsigned extend byte to word
    uxtb    Rd, Rm      ; Rd = (uint8_t)Rm

    ; signed extend byte to word
    sxtb    Rd, Rm      ; Rd = (int8_t)Rm

    ; unsigned extend halfword to word
    uxth    Rd, Rm      ; Rd = (uint16_t)Rm

    ; signed extend halfword to word
    sxth    Rd, Rm      ; Rd = (int16_t)Rm
</pre>
<p>You can optionally apply a rotation to the second register so that you can extract a 8-bit or 16-bit value that sits along a byte boundary.</p>
<pre>    ; unsigned/signed extend byte to word with rotation
    ; rotation must be a multiple of 8
    uxtb    Rd, Rm, #rot ; Rd = (uint8_t)(Rm ROR #rot)
    sxtb    Rd, Rm, #rot ; Rd = ( int8_t)(Rm ROR #rot)

    ; unsigned/signed extend halfword to word with rotation
    ; rotation must be a multiple of 8
    uxth    Rd, Rm, #rot ; Rd = (uint16_t)(Rm ROR #rot)
    sxth    Rd, Rm, #rot ; Rd = ( int16_t)(Rm ROR #rot)
</pre>
<p>It’s kind of weird to apply a 24-bit rotation to extract a halfword, but you can do it if you want to.</p>
<p>You can also zero-extend or sign-extend a word to a doubleword using instructions you already have available:</p>
<pre>    ; zero-extend Rd to Rd/R(d+1)
    mov     R(d+1), #0          ; set to 0

    ; sign-extend Rd to Rd/R(d+1)
    asrs    R(d+1), Rd, #31     ; copy sign bit to all bits
</pre>
<p>The trick is that a signed right-shift by 31 positions ends up filling the entire word with the sign bit. We use the S-version <code>ASRS</code> because it allows a compact 16-bit encoding if both the source and destination registers are low.</p>
<p>The <code>ASR #31</code> trick can also be used in the <code>op2</code> of arithmetic or logical instructions.</p>
<pre>    ; set r0 to zero if r1 is positive or zero
    and     r0, r1, ASR #31
</pre>
<p>The trick here is that <code>r1, ASR #31</code> produces <code>0xFFFFFFFF</code> if <var>r1</var> is negative, but <code>0x00000000</code> if <var>r1</var> is positive or zero.</p>
<p>In addition to the straight zero- and sign-extension operations, there are other instructions that combine the extension with another operation. Most of them are focused on multimedia scenarios, but the extend-and-add instructions are more general-purpose, and I have seen the compiler generate the versions with no rotation.</p>
<pre>    ; zero/sign extend and add byte with optional rotation
    ; rotation must be a multiple of 8
    uxtab   Rd, Rn, #rot        ; Rd = Rd + (uint8_t)(Rn ROR #rot)
    sxtab   Rd, Rn, #rot        ; Rd = Rd + ( int8_t)(Rn ROR #rot)

    ; zero/sign extend and add halfword with optional rotation
    ; rotation must be a multiple of 8
    sxtah   Rd, Rn, #rot        ; Rd = Rd + ( int16_t)(Rn ROR #rot)
    uxtah   Rd, Rn, #rot        ; Rd = Rd + (uint16_t)(Rn ROR #rot)
</pre>
<p>There’s another instruction that looks like it’d come in handy, particularly in Win32 user interface code that has to pack two 16-bit coordinates into a 32-bit integer, but I haven’t seen any compiler generate it:</p>
<pre>    ; pack halfword bottom-and-top, or top-and-bottom
    ; shift is optional
    pkhbt   Rd, Rn, Rm, LSL #imm ; Rd = ((Rm LSL #imm) &lt;&lt; 16) | (uint16_t)Rn
    pkhtb   Rd, Rn, Rm, ASR #imm ; Rd = (Rn &lt;&lt; 16) | (uint16_t)(Rm ASR #imm)
</pre>
<p>The bottom-and-top version puts the first input register in the bottom part of the output, and the second input parameter goes into the top part. The top-and-bottom version does it the other way. (The top-and-bottom instruction is not redundant because the barrel shifter can be applied only to the second input parameter.)</p>
<p>When the compiler needs to do this, it generates two instructions:</p>
<pre>    ; pack halfword bottom-and-top
    uxth    r12, Rn                 ; r12 = (uint16_t)Rn
    orr     Rd, r12, Rm, LSL #16    ; Rd = r12 | (Rm &lt;&lt; 16)
                                    ;    = (uint16_t)Rn | (Rm &lt;&lt; 16)
</pre>
<p>Even if it didn’t want to use <code>PKHBT</code>, it could have used <code>BFI</code> to pack the values in a single instruction:</p>
<pre>    ; pack halfword bottom-and-top (in place)
    bfi     Rd, Rm, #16, #16        ; Rd[31:16] = Rm[15:0]
</pre>
<p>Maybe there’s some dirty secret about the <code>PKHBT</code> and <code>BFI</code> instructions that the compiler knows but I don’t.</p>


</body>