<body style=max-width:80ch;margin:auto>
<h1 class="entry-title" style="margin-bottom:15px;">The AArch64 processor (aka arm64), part 11: Loading addresses</h1>  <!-- .entry-meta -->

<p>Loading the address of a variable can be tricky on AArch64, seeing as you have to somehow get a 64-bit value into a register, and you don’t even know the value until the module is loaded and relocated in memory.</p>
<p>The way to do this is with the help of the <code>ADR</code> and <code>ADRP</code> instructions.</p>
<pre>    ; form pc-relative address to nearby label (±1MB)
    ; Xn = pc + offset
    adr     Xn, label

    ; form pc-relative address to nearby 4KB page (±4GB)
    ; Xn = (pc &amp; ~4095) + (offset * 4096)
    adrp    Xn, label
</pre>
<p>The <code>ADR</code> instruction adds a 21-bit signed immediate to the current instruction’s address.</p>
<p>The <code>ADRP</code> instruction takes a 21-bit signed immediate, shifts it left 12 positions, and then adds it to the address of the starting byte of the page the current instruction is on. The result is the address of the starting byte of a page nearby.</p>
<p>Since modules are unlikely to be bigger than 4GB, the ±4GB reach should be enough to cover accesses to any global variables in the module from any code in that same module.</p>
<p>You can use <code>ADRP</code> with offset addressing to access any global variable in two instructions.</p>
<pre>    ; first, set x0 to point to the start of the page
    ; that holds the global
    adrp    x0, global

    ; then use a positive offset to access the global
    ldr     w0, [x0, #PageOffset(global)]
</pre>
<p>All of the register offset addressing modes support offsets up to 4095 (most go even higher), so you are sure to be able to get it in two instructions.</p>
<p>If all you need is the address of a global variable, rather that its value, then you can use an <code>ADD</code> instruction:</p>
<pre>    ; first, set x0 to point to the start of the page
    ; that holds the global
    adrp    x0, global

    ; then add the page offset to get the address of the global
    add     x0, x0, #PageOffset(global)
</pre>
<p>Both the unsigned offset in the <code>[Rn, #imm]</code> addressing mode as well as the unsigned immediate in the <code>add</code> instruction are 12 bits long, which is certainly-not-coincidentally exactly the number of bits in a page offset.</p>
<p>The Microsoft compiler goes to some effort to consolidate address calculations for global variables. For example, if it knows for sure that two global variables are laid out in memory that is a known distance apart, then it will use an <code>ADRP</code> to get the address of one, and then use that fixed offset from the first variable to get the second.</p>
<pre>// global variables
int a, b;

// code generation for calling
f(&amp;a, b);

    ; x0 points to start of page containing variable "a"
    adrp    x0, a

    ; adjust to point directly at "a"
    add     x0, a, #PageOffset(a)

    ; "b" is right next to "a", so load w1 via register offset
    ldr     w1, [x0, #8]

    ; ready to call "f"
    bl      f
</pre>
<p>(We’ll learn more about the calling convention later.)</p>
<p>Note that this optimization is not available if <code>a</code> and <code>b</code> were declared <code>extern</code>, since the compiler doesn’t know anything about the layout of the memory in that case.</p>
<p>Using <var>pc</var>-relative addresses makes it easier to generate position-independent code. I don’t know whether Windows requires AArch64 code to be position-independent, but doing so reduces the number of fixups needed, so it’s still a good thing even if not required.</p>


</body>