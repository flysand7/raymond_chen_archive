<body style=max-width:80ch;margin:auto>
<h1 class="entry-title" style="margin-bottom:15px;">The Alpha AXP, part 5: Conditional operations and control flow</h1>  <!-- .entry-meta -->

<p>The Alpha AXP has no flags register. Conditional operations are performed based on the current value of a general-purpose register. The conditions available on the Alpha AXP are the following: </p>
<table border="0" cellspacing="0">
<tr>
<td><code>EQ</code> </td>
<td>if zero</td>
</tr>
<tr>
<td><code>NE</code> </td>
<td>if not zero</td>
</tr>
<tr>
<td><code>GE</code> </td>
<td>if signed greater than or equal to zero</td>
</tr>
<tr>
<td><code>GT</code> </td>
<td>if signed greater than zero</td>
</tr>
<tr>
<td><code>LE</code> </td>
<td>if signed less than or equal to zero</td>
</tr>
<tr>
<td><code>LT</code> </td>
<td>if signed less than zero</td>
</tr>
<tr>
<td><code>LBC</code> </td>
<td>if low bit clear (if even)</td>
</tr>
<tr>
<td><code>LBS</code> </td>
<td>if low bit set (if odd)</td>
</tr>
</table>
<p>In the discussion below, the abbreviation <code><u>cc</u></code> represents one of the above condition codes. </p>
<p>The conditional move instructions test a source register against a condition, and if the condition is true, the destination register receives the second source. </p>
<pre>
    CMOV<u>cc</u>  Ra, Rb/#b, Rc   ; if Ra meets condition, then Rc = Rb/#b
</pre>
<p>You can also generate booleans from conditions. Note that the set of conditions here is not the same as the standard set of conditions above! </p>
<pre>
    CMPEQ   Ra, Rb/#b, Rc   ; Rc = (Ra == Rb/#b)
    CMPLT   Ra, Rb/#b, Rc   ; Rc = (Ra &lt; Rb/#b) signed comparison
    CMPLE   Ra, Rb/#b, Rc   ; Rc = (Ra ≤ Rb/#b) signed comparison
    CMPULT  Ra, Rb/#b, Rc   ; Rc = (Ra &lt; Rb/#b) unsigned comparison
    CMPULE  Ra, Rb/#b, Rc   ; Rc = (Ra ≤ Rb/#b) unsigned comparison
</pre>
<p>These comparison operators produce values of exactly 0 or 1, according to the result of the comparison, and the comparison is against the full 64-bit register value. </p>
<p>Conditional jump instructions provide a condition and a register, as well as a jump target. </p>
<pre>
    B<u>cc</u>     Ra, destination
</pre>
<p>where <code><u>cc</u></code> is one of the condition codes above. The instruction tests the specified register against the condition, and if true, control is transferred to the destination. The test is against the full 64-bit register value, and the destination is encoded as a 21-bit value, in units of instructions (4 bytes), which provides a reach of &amp;pm;4MB. </p>
<p>Conditional branches backward are predicted taken. Conditional branches forward are predicted not taken. </p>
<p>There are two types of unconditional branches. They are functionally the same but have different consequences for the return address predictor. </p>
<pre>
    BR      Ra, destination ; not expected to return
    BSR     Ra, destination ; expected to return
</pre>
<p>These instructions store the address of the subsequent instruction (the return address) in the <var>Ra</var> register and then transfer to the destination. The <code>BR</code> instruction does not push the return address onto the return address predictor stack; the <code>BSR</code> instruction does. </p>
<p>The <code>BR</code> instruction is typically used with <var>zero</var> as the register to receive the return address, since the value is almost always thrown away. (Recall that there is a special exemption for branch instructions to the usual rule that instructions which write to <var>zero</var> can be optimized away.) </p>
<p>The Win32 calling convention dictates that the <var>ra</var> register holds the return address on entry to a function. </p>
<p>There are four indirect jump instructions which are all functionally equivalent but differ in their effect on the return address predictor. </p>
<pre>
    JMP     Ra, (Rb), hint16    ; not expected to return
    JSR     Ra, (Rb), hint16    ; expected to return
    RET     Ra, (Rb), hint16    ; end of function
    JSR_CO  Ra, (Rb), hint16    ; coroutine
</pre>
<p>The <var>Ra</var> register receives the return address, typically <var>zero</var> in the case of <code>JMP</code> and <code>RET</code>, and conventionally <var>ra</var> in the case of <code>JSR</code>. As you have probably guessed, <code>JMP</code> has no effect on the return address predictor, <code>JSR</code> pushes the return address onto the predictor stack, and <code>RET</code> pops the return address off of the predictor stack and predicts a transfer to the popped value. The weird guy is <code>JSR_CO</code> which replaces the return address at the top of the predictor stack with the new return address and predicts a transfer to the old value. </p>
<p>The official name of <code>JSR_CO</code> is <code>JSR_</code><code>COROUTINE</code>, but it doesn’t really matter because I have never see <code>JSR_CO</code> in practice. </p>
<p>For the <code>JMP</code> and <code>JSR</code> instructions, the “hint” is a static prediction of the low 16 bits of the value in <var>Rb</var>. </p>
<p>The <code>RET</code> and <code>JSR_CO</code> instructions don’t need a hint because they have their own return address predictor. However, DEC recommends that the hint for a <code>RET</code> instruction be 1 for a return from a procedure, and 0 otherwise. We’ll see more about this another day. </p>
<p>The Microsoft compiler doesn’t generate hints; it just sets the hint to zero. Profile-guided optimization didn’t come to Visual C++ until after support for the Alpha AXP was dropped, but if it were still in support, I’m assuming that profile-guided optimization would have filled in the hint. </p>
<p>Non-virtual calls will look generally like this: </p>
<pre>
    ; Put the parameters in a0 through a5
    ; by whatever means appropriate.
    ; Excess parameters go on the stack.
    ; (Not shown here.)
    BIS     zero, s1, a0    ; copied from another register
    LDL     a1, 32(sp)      ; loaded from memory
    ADDL    zero, #1, a2    ; calculated in place

    BSR     ra, destination ; call the other function
    ; result is in the v0 register
</pre>
<p>Virtual calls load the destination from the target’s vtable: </p>
<pre>
    ; Put the parameters in a0 through a5
    ; by whatever means appropriate.
    ; Excess parameters go on the stack.
    ; (Not shown here.)
    ; "this" goes into a0.
    BIS     zero, s1, a0    ; copied from another register
    LDL     a1, 32(sp)      ; loaded from memory
    ADDL    zero, #1, a2    ; calculated in place

    LDL     t0, (a0)        ; load vtable
    LDL     t0, 8(t0)       ; load function from vtable
    BSR     ra, (t0)        ; call the function pointer
    ; result is in the v0 register
</pre>
<p>Calls to exported functions are indirect through a global variable, which means we need to get the address of that global. </p>
<pre>
    ; Put the parameters in a0 through a5
    ; by whatever means appropriate.
    ; Excess parameters go on the stack.
    ; (Not shown here.)
    BIS     zero, s1, a0    ; copied from another register
    LDL     a1, 32(sp)      ; loaded from memory
    ADDL    zero, #1, a2    ; calculated in place

    LDAH    t0, xxxx(zero)  ; 64KB block where global variable resides
    LDL     t0, yyyy(t0)    ; load the global variable
    BSR     ra, (t0)        ; call the function pointer
    ; result is in the v0 register
</pre>
<p>The above examples use the <code>LDL</code> instruction, which loads a register from memory. We’ll learn more about memory access next time. </p>


</body>