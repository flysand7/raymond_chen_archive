<body style=max-width:80ch;margin:auto>
<h1 class="entry-title" style="margin-bottom:15px;">The wrong way of benchmarking the most efficient integer comparison function</h1>  <!-- .entry-meta -->

<p>On StackOverflow, there’s a question about <a href="https://stackoverflow.com/q/10996418/902497">the most efficient way to compare two integers and produce a result suitable for a comparison function</a>, where a negative value means that the first value is smaller than the second, a positive value means that the first value is greater than the second, and zero means that they are equal. </p>
<p>There was much microbenchmarking of various options, ranging from the straightforward </p>
<pre>
int compare1(int a, int b)
{
    if (a &lt; b) return -1;
    if (a &gt; b) return 1;
    return 0;
}
</pre>
<p>to the clever </p>
<pre>
int compare2(int a, int b)
{
    return (a &gt; b) - (a &lt; b);
}
</pre>
<p>to the hybrid </p>
<pre>
int compare3(int a, int b)
{
    return (a &lt; b) ? -1 : (a &gt; b);
}
</pre>
<p>to inline assembly </p>
<pre>
int compare4(int a, int b)
{
    __asm__ __volatile__ (
        "sub %1, %0 \n\t"
        "jno 1f \n\t"
        "cmc \n\t"
        "rcr %0 \n\t"
        "1: "
    : "+r"(a)
    : "r"(b)
    : "cc");
    return a;
}
</pre>
<p>The benchmark pitted the comparison functions against each other by comparing random pairs of numbers and adding up the results to prevent the code from being optimized out. </p>
<p>But here’s the thing: Adding up the results is completely unrealistic. </p>
<p>There are no meaningful semantics that could be applied to a sum of numbers for which only the sign is significant. No program that uses a comparison function will add the results. The only thing you can do with the result is compare it against zero and take one of three actions based on the sign. </p>
<p>Adding up all the results means that you’re not using the function in a realistic way, which means that your benchmark isn’t realistic. </p>
<p>Let’s try to fix that. Here’s my alternative test: </p>
<pre>
// Looks for "key" in sorted range [first, last) using the
// specified comparison function. Returns iterator to found item,
// or last if not found.

template&lt;typename It, typename T, typename Comp&gt;
It binarySearch(It first, It last, const T&amp; key, Comp compare)
{
 // invariant: if key exists, it is in the range [first, first+length)
 // This binary search avoids the <a href="https://research.googleblog.com/2006/06/extra-extra-read-all-about-it-nearly.html">integer overflow problem</a>
 // by operating on lengths rather than ranges.
 auto length = last - first;
 while (length &gt; 0) {
  auto step = length / 2;
  It it = first + step;
  auto result = compare(*it, key);
  if (result &lt; 0) {
   first = it + 1;
   length -= step + 1;
  } else if (result == 0) {
   return it;
  } else {
   length = step;
  }
 }
 return last;
}

int main(int argc, char **argv)
{
 // initialize the array with sorted even numbers
 int a[8192];
 for (int i = 0; i &lt; 8192; i++) a[i] = i * 2;

 for (int iterations = 0; iterations &lt; 1000; iterations++) {
  int correct = 0;
  for (int j = -1; j &lt; 16383; j++) {
   auto it = binarySearch(a, a+8192, j, COMPARE);
   if (j &lt; 0 || j &gt; 16382 || j % 2) correct += it == a+8192;
   else correct += it == a + (j / 2);
  }
  // if correct != 16384, then we have a bug somewhere
  if (correct != 16384) return 1;
 }
 return 0;
}
</pre>

<p>Let’s look at the code generation for the various comparison functions. I used <a href="https://gcc.godbolt.org/">gcc.godbolt.org</a> with x86-64 gcc 7.2 and optimization <code>-O3</code>. </p>
<p>If we try <code>compare1</code>, then the binary search looks like this: </p>
<pre>
    ; on entry, esi is the value to search for

    lea rdi, [rsp-120]          ; rdi = first
    mov edx, 8192               ; edx = length
    jmp .L9
.L25:                           ; was greater than
    mov rdx, rax                ; length = step
    test rdx, rdx               ; while (length &gt; 0)
    jle .L19
.L9:
    mov rax, rdx                ;
    sar rax, 1                  ; eax = step = length / 2
    lea rcx, [rdi+rax*4]        ; it = first + step

    <font color="blue">; result = compare(*it, key), and then test the result
    cmp dword ptr [rcx], esi    ; compare(*it, key)
    jl .L11                     ; if less than
    jne .L25                    ; if not equal (therefore if greater than)</font>
    ... return value in rcx     ; if equal, answer is in rcx

.L11:                           ; was less than
    add rax, 1                  ; step + 1
    lea rdi, [rcx+4]            ; first = it + 1
    sub rdx, rax                ; length -= step + 1
    test rdx, rdx               ; while (length &gt; 0)
    jg .L9
.L19:
    lea rcx, [rsp+32648]        ; rcx = last
    ... return value in rcx
</pre>
<p><b>Exercise</b>: Why is <code>rsp - 120</code> the start of the array? </p>
<p>Observe that despite using the lamest, least-optimized comparison function, we got the comparison-and-test code that is much what we would have written if we had done it in assembly language ourselves: We compare the two values, and then follow up with two branches based on the same shared flags. The comparison is still there, but the calculation and testing of the return value are gone. </p>
<p>In other words, not only was <code>compare1</code> optimized down to one <code>cmp</code> instruction, but it also managed to delete instructions from the <code>binarySearch</code> function too. It had a net cost of negative instructions! </p>
<p>What happened here? How did the compiler manage to optimize out all our code and leave us with the shortest possible assembly language equivalent? </p>
<p>Simple: First, the compiler did some constant propagation. After inlining the <code>compare1</code> function, the compiler saw this: </p>
<pre>
    int result;
    if (*it &lt; key) result = -1;
    else if (*it &gt; key) result = 1;
    else result = 0;
    if (result &lt; 0) {
      ... less than ...
    } else if (result == 0) {
      ... equal to ...
    } else {
      ... greater than ...
    }
</pre>
<p>The compiler realized that it already knew whether constants were greater than, less than, or equal to zero, so it could remove the test against <code>result</code> and jump straight to the answer: </p>
<pre>
    int result;
    if (*it &lt; key) { result = -1; <font color="blue">goto less_than;</font> }
    else if (*it &gt; key) { result = 1; <font color="blue">goto greater_than;</font> }
    else { result = 0; <font color="blue">goto equal_to;</font> }
    if (result &lt; 0) {
less_than:
      ... less than ...
    } else if (result == 0) {
equal_to:
      ... equal to ...
    } else {
greater_than:
      ... greater than ...
    }
</pre>
<p>And then it saw that all of the tests against <code>result</code> were unreachable code, so it deleted them. </p>
<pre>
    int result;
    if (*it &lt; key) { result = -1; <font color="blue">goto less_than;</font> }
    else if (*it &gt; key) { result = 1; <font color="blue">goto greater_than;</font> }
    else { result = 0; <font color="blue">goto equal_to;</font> }

less_than:
      ... less than ...
      <font color="blue">goto done;</font>

equal_to:
      ... equal to ...
      <font color="blue">goto done;</font>

greater_than:
      ... greater than ...
<font color="blue">done:</font>
</pre>
<p> That then left <code>result</code> as a write-only variable, so it too could be deleted: </p>
<pre>
    if (*it &lt; key) { <font color="blue">goto less_than;</font> }
    else if (*it &gt; key) { <font color="blue">goto greater_than;</font> }
    else { <font color="blue">goto equal_to;</font> }

less_than:
      ... less than ...
      <font color="blue">goto done;</font>

equal_to:
      ... equal to ...
      <font color="blue">goto done;</font>

greater_than:
      ... greater than ...
<font color="blue">done:</font>
</pre>
<p>Which is equivalent to the code we wanted all along: </p>
<pre>
    if (*it &lt; key) {
      ... less than ...
    } else if (*it &gt; key) {
      ... greater than ...
    } else {
      ... equal to ...
    }
</pre>
<p>The last optimization is realizing that the test in the <code>else if</code> could use the flags left over by the <code>if</code>, so all that was left was the conditional jump. </p>
<p>Some very straightforward optimizations took our very unoptimized (but easy-to-analyze) code and turned it into something much more efficient. </p>
<p>On the other hand, let’s look at what happens with, say, the second comparison function: </p>
<pre>
    ; on entry, edi is the value to search for

    lea r9, [rsp-120]           ; r9 = first
    mov ecx, 8192               ; ecx = length
    jmp .L9
.L11:                           ;
    <font color="blue">test eax, eax               ; result == 0?
    je .L10                     ; Y: found it</font>
                                ; was greater than
    mov rcx, rdx                ; length = step
    test rcx, rcx               ; while (length &gt; 0)
    jle .L19
.L9:
    mov rdx, rcx
    <font color="blue">xor eax, eax                ; return value of compare2</font>
    sar rdx, 1                  ; rdx = step = length / 2
    lea r8, [r9+rdx*4]          ; it = first + step

    <font color="blue">; result = compare(*it, key), and then test the result
    mov esi, dword ptr [r8]     ; esi = *it
    cmp esi, edi                ; compare *it with key
    setl sil                    ; sil = 1 if less than
    setg al                     ; al  = 1 if greater than
                                ; eax = 1 if greater than
    movzx esi, sil              ; esi = 1 if less than
    sub eax, esi                ; result = (greater than) - (less than)
    cmp eax, -1                 ; less than zero?
    jne .L11                    ; N: Try zero or positive</font>

                                ; was less than
    add rdx, 1                  ; step + 1
    lea r9, [r8+4]              ; first = it + 1
    sub rcx, rdx                ; length -= step + 1
    test rcx, rcx               ; while (length &gt; 0)
    jg .L9
.L19:
    lea r8, [rsp+32648]         ; r8 = last
.L10:
    ... return value in r8
</pre>
<p>The second comparison function <code>compare2</code> uses the relational comparison operators to generate exactly 0 or 1. This is a clever way of generating <code>-1</code>, <code>0</code>, or <code>+1</code>, but unfortunately,  that was not our goal in the grand scheme of things. It was merely a step toward that goal. The way that <code>compare2</code> calculates the result is too complicated for the optimizer to understand, so it just does its best at calculating the formal return value from <code>compare2</code> and testing its sign. (The compiler does realize that the only possible negative value is <code>-1</code>, but that’s not enough insight to let it optimize the entire expression away.) </p>
<p>If we try <code>compare3</code>, we get this: </p>
<pre>
    ; on entry, esi is the value to search for

    lea rdi, [rsp-120]          ; rdi = first
    mov ecx, 8192               ; ecx = length
    jmp .L12
.L28:                           ; was greater than
    mov rcx, rax                ; length = step
.L12:
    mov rax, rcx
    sar rax, 1                  ; rax = step = length / 2
    lea rdx, [rdi+rax*4]        ; it = first + step

    <font color="blue">; result = compare(*it, key), and then test the result
    cmp dword ptr [rdx], esi    ; compare(*it, key)
    jl .L14                     ; if less than
    jle .L13                    ; if less than or equal (therefore equal)</font>

    ; "length" is in eax now
.L15:                           ; was greater than
    test eax, eax               ; length == 0?
    jg .L28                     ; N: continue looping
    lea rdx, [rsp+32648]        ; rdx = last
.L13:
    ... return value in rdx

.L14:                           ; was less than
    add rax, 1                  ; step + 1
    lea rdi, [rdx+4]            ; first = it + 1
    sub rcx, rax                ; length -= step + 1
    mov rax, rcx                ; rax = length
    jmp .L15
</pre>
<p>The compiler was able to understand this version of the comparison function: It observed that if <code>a &lt; b</code>, then the result of <code>compare3</code> is always negative, so it jumped straight to the less-than case. Otherwise, it observed that the result was zero if <code>a</code> is not greater than <code>b</code> and jumped straight to that case too. The compiler did have some room for improvement with the placement of the basic blocks, since there is an unconditional jump in the inner loop, but overall it did a pretty good job. </p>
<p>The last case is the inline assembly with <code>compare4</code>. As you might expect, the compiler had the most trouble with this. </p>
<pre>
    ; on entry, edi is the value to search for

    lea r8, [rsp-120]           ; r8 = first
    mov ecx, 8192               ; ecx = length
    jmp .L12
.L14:                           ; zero or positive
    <font color="blue">je .L13                     ; zero - done</font>
                                ; was greater than
    mov rcx, rdx                ; length = step
    test rcx, rcx               ; while (length &gt; 0)
    jle .L22
.L12:
    mov rdx, rcx
    sar rdx, 1                  ; rdx = step = length / 2
    lea rsi, [r8+rdx*4]         ; it = first + step

    <font color="blue">; result = compare(*it, key), and then test the result
    mov eax, dword ptr [rsi]    ; eax = *it
    sub eax, edi
    jno 1f
    cmc
    rcr eax, 1
1:
    test eax, eax               ; less than zero?
    jne .L14                    ; N: Try zero or positive</font>

                                ; was less than
    add rdx, 1                  ; step + 1
    lea r8, [rsi+4]             ; first = it + 1
    sub rcx, rdx                ; length -= step + 1
    test rcx, rcx               ; while (length &gt; 0)
    jg .L12
.L22:
    lea rsi, [rsp+32648]        ; rsi = last
.L13:
    ... return value in rsi
</pre>
<p>This is pretty much the same as <code>compare2</code>: The compiler has no insight at all into the inline assembly, so it just dumps it into the code like a black box, and then once control exits the black box, it checks the sign in a fairly efficient way. But it had no real optimization opportunities because you can’t really optimize inline assembly. </p>
<p>The conclusion of all this is that optimizing the instruction count in your finely-tuned comparison function is a fun little exercise, but it doesn’t necessarily translate into real-world improvements. In our case, we focused on optimizing the code that encodes the result of the comparison without regard for how the caller is going to decode that result. The contract between the two functions is that one function needs to package some result, and the other function needs to unpack it. But we discovered that the more obtusely we wrote the code for the packing side, the less likely the compiler would be able to see how to optimize out the entire hassle of packing and unpacking in the first place. In the specific case of comparison functions, it means that you may want to return <code>+1</code>, <code>0</code>, and <code>-1</code> explicitly rather than calculating those values in a fancy way, because it turns out compilers are really good at optimizing “compare a constant with zero”. </p>
<p>You have to see how your attempted optimizations fit into the bigger picture because you may have hyper-optimized one part of the solution to the point that it prevents deeper optimizations in other parts of the solution. </p>
<p><b>Bonus chatter</b>: If the comparison function is not inlined, then all of these optimization opportunities disappear. But I personally wouldn’t worry about it too much, because if the comparison function is not inlined, then the entire operation is going to be dominated by the function call overhead: Setting up the registers for the call, making the call, returning from the call, testing the result, and most importantly, the lost register optimization opportunities not only because the compiler loses opportunities to enregister values across the call, but also because the compiler has to protect against the possibility that the comparison function will mutate global state and consequently create aliasing issues. </p>


</body>