<body style=max-width:80ch;margin:auto>
<h1 class="entry-title" style="margin-bottom:15px;">Exiting a batch file without exiting the command shell -and- batch file subroutines</h1>  <!-- .entry-meta -->

<p>
Prepare your party hats: Batch File Week is almost over.
</p>
<p>
In your batch file,
you may want to exit batch file processing
(say, you encountered an error and want to give up),
but if you use the <code>exit</code> command,
that will exit the entire command processor.
Which is probably not what you intended.
</p>
<p>
Batch file processing ends when execution reaches
the end of the batch file.
The trick therefore is to use the <code>goto</code> command
to jump to a label right before the end of the file,
so that execution “falls off the end”.
</p>
<pre>
@echo off
if "%1"=="" echo You must provide a file name.&amp;goto end
if NOT EXIST "\\server\backup\%USERNAME%\<a href="http://blogs.msdn.com/b/oldnewthing/archive/2003/10/22/55388.aspx">nul</a>" mkdir "\\server\backup\%USERNAME%"
if NOT EXIST "\\server\backup\%USERNAME%\nul" echo Unable to create output directory.&amp;goto end
copy "%1" "\\server\backup\%USERNAME%"
:end
</pre>
<p>
Here, there are two places where we abandon batch file execution.
One is on an invalid parameter,
and another is if the output directory couldn’t be created
(or if it isn’t a directory at all).
</p>
<p>
The batch command interpreter provides a courtesy label
to simply this technique:
The special goto target
<code>goto :eof</code> (with the colon)
jumps to the end of the batch file.
It’s as if every batch file had a hidden goto label
called <code>:eof</code> on the very last line.
</p>
<p>
The <code>goto :eof</code> trick becomes even more handy
when you start playing with batch file subroutines.
Okay, let's back up: Batch file subroutines?
</p>
<p>
By using the <code>call</code> command,
a batch file can invoke another batch file and regain
control after that other batch file returns.
(If you forget the <code>call</code>, then control
does not return. In other words, the default mode
for batch file invocation is <i>chain</i>.)
In other words, the <code>call</code> command
lets you invoke another batch file as a subroutine.
The command line parameters are received by the
other batch file as the usual numbered parameters
<code>%1</code>, <code>%2</code>, <i>etc</i>.
</p>
<p>
It's annoying having to put every subroutine inside
its own batch file,
so the command interpreter folks added a way to
call a subroutine <i>inside the same batch file</i>.
The syntax for this is
<code>call :label parameter parameter parameter</code>.
This is logically equivalent to a batch file recursively
calling itself, except that execution begins at the
specified label instead of the first line of the file.
(It's as if a secret <code>goto label</code> were added
to the top of the file.)
</p>
<p>
And since it is a batch file, execution of the called
subroutine ends when execution falls off the end of the file.
And that's where the special <code>goto</code> target
comes in handy.
At the end of your subroutine,
you can jump to the end of the batch file (so that
execution falls off the end) by doing a
<code>goto :eof</code>.
</p>
<p>
In other words,
<code>goto :eof</code> is the <code>return</code> statement
for batch file subroutines.
</p>
<p>
Let's take it for a spin:
</p>
<pre>
@echo off
call :subroutine a b c
call :subroutine d e f
goto :eof
:subroutine
echo My parameters are 1=%1, 2=%2, 3=%3
goto :eof
</pre>
<p>
That final <code>goto :eof</code> is redundant,
but it's probably a good habit to get into,
like putting a <code>break;</code> at the end of
your last <code>case</code>.
</p>
<p>
The subroutine technique is handy even if you don't
really care about the subroutine,
because stashing the arguments into the <code>%n</code>
parameters lets you use the
<a href="http://www.microsoft.com/resources/documentation/windows/xp/all/proddocs/en-us/percent.mspx">
tilde operators</a>
to process the inbound parameter.
</p>
<pre>
@echo off
call :printfilesize "C:\Program Files\Windows NT\Accessories\wordpad.exe"
goto :eof
:printfilesize
echo The size of %1 is %~z1
goto :eof
</pre>
<p>
Okay, this isn't actually much of a handy trick because you can also
do it without a subroutine:
</p>
<pre>
@echo off
for %%i ^
in ("C:\Program Files\Windows NT\Accessories\wordpad.exe") ^
do echo The size of %%i is %%~zi
</pre>
<p>
On the other hand, the subroutine trick combines well with the
<code>FOR</code> command,
since it lets you put complex content in the loop body
without having to mess with
<a href="http://blogs.msdn.com/b/oldnewthing/archive/2006/08/23/714650.aspx">
delayed expansion</a>:
</p>
<pre>
@echo off
setlocal
set DISKSIZE=1474560
set CLUSTER=512
set DISKS=1
set TOTAL=0
for %%i in (*) do call :onefile "%%i"
set /a DISKS=DISKS+1
echo Total disks required: %DISKS%
endlocal
goto :eof
:onefile
set /a SIZE=((%~z1 + CLUSTER - 1) / CLUSTER) * CLUSTER
if %SIZE% GEQ %DISKSIZE% (
    echo File %1 does not fit on a floppy - skipped
    goto :eof
)
set /a TOTAL=TOTAL+SIZE
if %TOTAL% GEQ %DISKSIZE% (
    echo ---- need another disk
    set /a DISKS=DISKS+1
    set /a TOTAL=SIZE
)
echo copy %1
goto :eof
</pre>
<p>
This program calculates the number of floppy disks it would take
to copy the contents of the current directory without compression.
</p>
<p>
The <code>setlocal</code> command takes a snapshot of the
environment for restoration when we perform the <code>endlocal</code>
at the end.
That will clean up our temporary variables when we're done.
</p>
<p>
The first two variables are parameters for the calculation,
namely the disk capacity and the cluster size.
(We're assuming that the root directory can hold all the files
we may ultimately copy.
Hey, this is just a demonstration, not a real program.)
</p>
<p>
The next two variables are our running total of the number of
disks we've used so far,
and how many bytes we've used on the last disk.
</p>
<p>
The <code>for</code> command iterates over all the files in the
current directory.
For each one, we call <code>:onefile</code> with the file name.
</p>
<p>
The <code>:onefile</code> subroutine does all the real work.
First, it takes the file size <code>%~z1</code> and rounds it up
to the nearest cluster.
It then sees if that size is larger than a floppy disk;
if so, then we're doomed, so we just skip the file.
Otherwise, we add the file to the current disk and see if it fits.
If not, then we declare the disk full and put the file on a brand
new disk.
</p>
<p>
After the loop is complete,
we print the number of floppy disks we calculated.
</p>
<p>
(This algorithm erroneously reports that no files require one disk.
Fixing that is left as an exercise.)
</p>
<p>
There's your quick introduction to the secret
<code>:eof</code> label and batch file subroutines.
</p>
<p>
[Raymond is currently away; this message was pre-recorded.]</p>


</body>