<body style=max-width:80ch;margin:auto>
<h1 class="entry-title" style="margin-bottom:15px;">The MIPS R4000, part 9: Branch delay slot parlor tricks</h1>  <!-- .entry-meta -->

<p>Last time, we learned about <a href="/code/the-mips-r4000-part-8-control-transfer"> the MIPS branch delay slot</a>. Today, we’ll look at some tricks you can play with the branch delay slot.</p>
<p>First trick: It is legal to jump into a branch delay slot. Of course, it’s not a branch delay slot when you do that. This lets you write some wacky-looking code:</p>
<pre>    B       somewhere           ; unconditional branch
label:
    OR      v0, zero, zero      ; v0 = 0
...
</pre>
<p>When the unconditional branch is taken, the <var>v0</var> register is set to zero before execution continues at the branch destination.</p>
<p>Meanwhile, if somebody jumps to <code>label</code>, then execution continues at <code>label</code>, which sets <var>v0</var> to zero, and then continues with other stuff.</p>
<p>The instruction at <code>label</code> acts both as the branch delay slot for the unconditional branch that precedes it, but it’s also the first instruction in the basic block if somebody jumps directly into it.</p>
<p>I’ve seen the opportunity arise for this sort of “squeeze out a single instruction” optimization, but the Microsoft compiler doesn’t take advantage of it. Which is probably a good thing. (For one thing, it makes it much harder for <a href="https://web.archive.org/web/20180719082446/https://blogs.msdn.microsoft.com/reiley/2011/08/06/microsoft-binary-technologies-and-debugging/"> binary transformation tools</a> to decompose a program into basic blocks and recombine them in different ways.)</p>
<p>Another branch delay slot trick is editing the return address as part of the jump.</p>
<pre>    BAL     somewhere
    ADDIU   ra, ra, 4

    NOP

    NOP ; the routine returns here!
</pre>
<p>The <code>BAL</code> instruction sets the <var>ra</var> register to point to the instruction after the branch delay slot, which in our case is the first <code>NOP</code>. But in the branch delay slot, we modify the <var>ra</var> register, so that when execution reaches the start of the called procedure, it gets an artificial return address.</p>
<p>I’m told this sort of trick is used by some compilers to combine a call and an unconditional jump into a call with fake return address. For example, in this code fragment</p>
<pre>    if (...) {
        ...
        function1(...);
    } else {
        ...
    }
    // resume
    x = 0;
</pre>
<p>the call to <code>function1</code> is probably followed by an unconditional jump to skip over the <code>else</code> branch.</p>
<pre>    BAL     function1
    NOP                     ; garbage in the branch delay slot
    B       resume
    OR      v0, zero, zero  ; set x = 0

    ... else-branch code goes here ...

    OR      v0, zero, zero  ; set x = 0
resume:
    ...
</pre>
<p>A sneaky compiler could <a href="http://www.pagetable.com/?p=313"> generate the following code</a>:</p>
<pre>    BAL     function1
    <span style="color: blue;">ADDIU   ra, ra, resume - nominal_return</span> ; tweak return address
nominal_return:

    ... else-branch code goes here ...

resume:
    OR      v0, zero, zero  ; set x = 0
    ...
</pre>
<p>In the branch delay slot, we edit the return address so that when <code>function1</code> returns, it resumes execution at <code>resume</code> rather than <code>nominal_return</code>, thereby avoiding having to execute another branch instruction. (We also were able to remove the duplicate <code>OR v0, zero, zero</code> instruction that had been hoisted into the branch delay slot of the unconditional branch.) Note that you get this savings only because you had a garbage <code>NOP</code> in the branch delay slot. If there were a useful instruction there, then the transformation would go like this:</p>
<pre>    // original code
    BAL     function1
    MOVE    a0, r0      ; set parameter for function
    B       resume
    OR      v0, zero, zero  ; set x = 0

    // sneaky code
    MOVE    a0, r0      ; set parameter for function
    BAL     function1
    ADDU    ra, ra, ... ; tweak return address

resume:
    OR      v0, zero, zero  ; set x = 0
</pre>
<p>The instruction in the <code>BAL</code> instruction’s branch delay slot would have to go somewhere else, so you didn’t save any time (though you still saved one instruction of space by avoiding duplication of the <code>OR v0, zero, zero</code>).</p>
<p>But as we saw earlier, <a href="https://devblogs.microsoft.com/oldnewthing/20041216-00/?p=36973"> this trick defeats the return address predictor</a>,¹ so it’s probably a bad idea.</p>
<p>Okay, next time, we’re going to look at the calling convention a bit more closely.</p>
<p><b>Bonus chatter</b>: Another extra sneaky trick is reusing the return address. Suppose your interpreter loop goes like this:</p>
<pre>void interpreter_loop(interpreter_state* state)
{
 for (;;) {
  uint32_t opcode = *state-&gt;pc;
  state-&gt;pc++;
  jump_table[opcode](state, opcode, state-&gt;pc);
 }
}
</pre>
<p>The interpreter loop just dispatches to the next opcode forever. Presumably you would break out of this loop with a <code>longjmp</code> or some other nonlocal transfer.</p>
<p>The handler function is given the current interpreter state (so it can update it), and as a courtesy, it also gets the current opcode and a pointer to the next unparsed byte as a convenience.</p>
<pre>interpreter_loop:
    ...
    MOVE    s0, a0       ; s0 points to the interpreter state
    LA      s1, jump_table
    LA      ra, next_opcode ; Footnote ²
next_opcode:
    LW      v1, 80(s0)  ; get address of next opcode byte
    ADDU    a2, v1, 1   ; move to next opcode byte (also argument for handler)
    LBU     a1, 0(v1)   ; load current opcode byte (also argument for handler)
    SW      a2, 80(s0)  ; save pointer to next opcode byte
    SLL     t0, a1, 2   ; multiple by 4 to index jump table
    ADDU    t0, t0, s1  ; calculate entry in jump table
    LW      v0, 0(t0)   ; load the jump target
    JR      v0          ; jump to handler - will return to next_opcode
    MOVE    a0, s0      ; argument for handler
</pre>
<p>When we call the first handler, <var>ra</var> is set equal to <code>next_opcode</code>. That handler will do its work and then return to the caller by restoring the return address to the <var>ra</var> register and performing a <code>JR ra</code>.</p>
<p>This means that when control returns to <code>next_opcode</code>, you know that <var>ra</var> is equal to <code>next_opcode</code>! Since that’s the value you wanted to be in that register anyway, you can just leave it there when you jump to the next handler, saving you the trouble of having to branch back up to <code>next_opcode</code> explicitly.</p>
<p>This seems to be a really clever trick, but it is probably not that useful in practice because of that return address predictor thing.</p>
<p>¹ On the other hand, the MIPS R4000 does not have separate opcodes for “jump indirect to register” and “jump indirect to register for the purpose of returning”; it uses the <code>JR</code> instruction for both cases.</p>
<p>The inability to distinguish whether a jump instruction was semantically a return instruction was a non-issue in the original implementation of the MIPS architecture. It had only a two-stage pipeline, so the single branch delay slot was sufficient to avoid ever needing to predict any branches at all.</p>
<p>The MIPS R4000 had a four-stage pipeline, and a branch misprediction would consequently suffer a 2-cycle stall. The MIPS designers codified existing practice and retroactively declared that if the register operand in the <code>JR</code> instruction is <var>ra</var>, then it predicts as a subroutine return; otherwise it predicts as a computed jump.</p>
<p>² For extra sneakiness (and to save an instruction),³ the loop preparation code could have been written as</p>
<pre>    LA      s1, jump_table
    BAL     next_opcode
    MOVE    s0, a0       ; s0 points to the interpreter state
next_opcode:
</pre>
<p>This version lets the processor calculate the address of <code>next_opcode</code> by performing a <code>BAL</code>. This sets the return address to the instruction after the branch delay slot, which is <code>next_opcode</code>, and then jumps to… <code>next_opcode</code>, which is where the instruction would have gone anyway.</p>
<p>³ Mind you, this size savings costs you a pipeline stall. See footnote 1.</p>


</body>