<body style=max-width:80ch;margin:auto>
<h1 class="entry-title" style="margin-bottom:15px;">The Alpha AXP, part 8: Memory access, storing bytes and words and unaligned data</h1>  <!-- .entry-meta -->

<p>Storing a byte and word requires a series of three operations: Read the original data, modify the original data to incorporate the byte or word, then write the modified data back to memory. </p>
<p>To assist with the modification are two groups of instructions known as insertion and masking. </p>
<pre>
    INSBL   Ra, Rb/#b, Rc  ; Rc =  (uint8_t)Ra &lt;&lt; (Rb/#b * 8 % 64)
    INSWL   Ra, Rb/#b, Rc  ; Rc = (uint16_t)Ra &lt;&lt; (Rb/#b * 8 % 64)
    INSLL   Ra, Rb/#b, Rc  ; Rc = (uint32_t)Ra &lt;&lt; (Rb/#b * 8 % 64)
    INSQL   Ra, Rb/#b, Rc  ; Rc = (uint64_t)Ra &lt;&lt; (Rb/#b * 8 % 64)

    INSWH   Ra, Rb/#b, Rc  ; Rc = (uint16_t)Ra &gt;&gt; ((64 - Rb/#b * 8) % 64)
    INSLH   Ra, Rb/#b, Rc  ; Rc = (uint32_t)Ra &gt;&gt; ((64 - Rb/#b * 8) % 64)
    INSQH   Ra, Rb/#b, Rc  ; Rc = (uint64_t)Ra &gt;&gt; ((64 - Rb/#b * 8) % 64)
</pre>
<p>These are the inverse of the extraction instructions. Instead of extracting data from a 128-bit value, they move the data into position within a 128-bit value. For example, here’s a diagram of inserting the long <code>FGHI</code> into a 128-bit value: </p>
<pre>
    high part  low part
    --------- ---------
    0000 0FGH           -- INSLH
              I000 0000 -- INSLL
</pre>
<p>The last piece of the puzzle is the masking instructions. </p>
<pre>
    MSKBL   Ra, Rb/#b, Rc  ; Rc = Ra &amp; ~( (uint8_t)~0 &lt;&lt; (Rb/#b * 8 % 64))
    MSKWL   Ra, Rb/#b, Rc  ; Rc = Ra &amp; ~((uint16_t)~0 &lt;&lt; (Rb/#b * 8 % 64))
    MSKWL   Ra, Rb/#b, Rc  ; Rc = Ra &amp; ~((uint32_t)~0 &lt;&lt; (Rb/#b * 8 % 64))
    MSKWL   Ra, Rb/#b, Rc  ; Rc = Ra &amp; ~((uint64_t)~0 &lt;&lt; (Rb/#b * 8 % 64))

    MSKWH   Ra, Rb/#b, Rc  ; Rc = Ra &amp; ~((uint16_t)~0 &gt;&gt; ((64 - Rb/#b * 8) % 64))
    MSKWH   Ra, Rb/#b, Rc  ; Rc = Ra &amp; ~((uint32_t)~0 &gt;&gt; ((64 - Rb/#b * 8) % 64))
    MSKWH   Ra, Rb/#b, Rc  ; Rc = Ra &amp; ~((uint64_t)~0 &gt;&gt; ((64 - Rb/#b * 8) % 64))
</pre>
<p>These instructions zero out the bytes of a 128-bit value that are about to be replaced by an insertion. </p>
<p>For example, here’s how the masking of a long would work: </p>
<pre>
    high part  low part
    --------- ---------
    ABCD EFGH IJKL MNOP -- 16-byte value
          ^^^ ^         -- 4 bytes to be inserted here
    ABCD E000           -- MSKLH
              0JKL MNOP -- MSKLL
</pre>
<p>Putting the pieces together, we see that in order to replace a long in the middle of a 128-bit value, you would use the insertion instructions to place the new value in the correct position, the masking instructions to zero out the bits that used to be there, and then “or” the pieces together. </p>
<pre>
    ; store an unaligned long in t1 to (t0)
    ; first read the 128-bit value currently in memory
    LDQ_U   t2,3(t0)                    ; t2 = yyyy yyyD
    LDQ_U   t5,(t0)                     ; t5 =           CBAx xxxx

    ; build the values to insert
    INSLH   t1,t0,t4                    ; t4 = 0000 000d
    INSLL   t1,t0,t3                    ; t3 =           cba0 0000

    ; mask out the values to be replaced
    MSKLH   t2,t0,t2                    ; t2 = yyyy yyy0
    MSKLL   t5,t0,t5                    ; t5 =           000x xxxx

    ; "or" the new values into place
    BIS     t2,t4,t2                    ; t2 = yyyy yyyd
    BIS     t5,t3,t5                    ; t5 =           cbax xxxx

    ; and write the results back out
    STQ_U   t2,3(t0)                    ; must store high then low
    STQ_U   t5,(t0)                     ; in case there was no straddling
</pre>
<p>Extending this pattern to quads and words is left as an exercise. </p>
<p>Notice that in the case where <var>t0</var> does not straddle two quads, we perform two reads from the same location, and two writes to the same location. Let’s walk through what happens: </p>
<pre>
    ; first read the 128-bit value currently in memory
    ; (which is really the same 64-bit value twice)
    LDQ_U   t2,3(t0)                    ; t2 = yyDC BAxx
    LDQ_U   t5,(t0)                     ; t5 = yyDC BAxx

    ; build the values to insert
    INSLH   t1,t0,t4                    ; t4 = 00dc ba00
    INSLL   t1,t0,t3                    ; t3 = 0000 0000

    ; mask out the values to be replaced
    MSKLH   t2,t0,t2                    ; t2 = yy00 00xx
    MSKLL   t5,t0,t5                    ; t5 = yyDC BAxx

    ; "or" the new values into place
    BIS     t2,t4,t2                    ; t2 = yydc baxx
    BIS     t5,t3,t5                    ; t5 = yyDC BAxx

    ; and write the results back out
    STQ_U   t2,3(t0)                    ; write same value back
    STQ_U   t5,(t0)                     ; write updated value
</pre>
<p>This highlights some of the weird memory effects of the Alpha AXP. If another thread snuck in and modified the memory at <var>t0 &amp; ~7</var>, those changes would be reverted at the first <code>STQ_U</code>, and then the updated value gets written next. This means that the value changes from <code>yyyyDCBAxx</code> to <code>zzzzDCBAww</code>, and then back to <code>yyyyDCBAxx</code>, and then finally to <code>yyyydcbaxx</code>. The value changes, and then appears to change back to the old value, before finally being updated to a new (sort-of) value. </p>
<p>We’ll learn more about the Alpha AXP memory model later. </p>
<p>In the case where you are writing a word and you know that it is aligned, then you can avoid having to deal with the 128-bit value and operate within a 64-bit value (because an aligned word will never straddle two quads). </p>
<pre>
    ; store an aligned word in t1 to (t0)
    ; first read the 64-bit value currently in memory
    LDQ_U   t5,(t0)                     t5 = yyBA xxxx

    ; build the value to insert
    INSWL   t1,t0,t3                    t3 = 00ba 0000

    ; mask out the values to be replaced
    MSKWL   t5,t0,t5                    t5 = yy00 xxxx

    ; "or" the new values into place
    BIS     t5,t3,t5                    t5 = yyba xxxx

    ; and write the results back out
    STQ_U   t5,(t0)
</pre>
<p>Okay, but what about bytes? Well, bytes can never be misaligned, so we always go through the “known aligned” shortcut. </p>
<pre>
    ; store a byte in t1 to (t0)
    ; first read the 64-bit value currently in memory
    LDQ_U   t5,(t0)                     t5 = yyyA xxxx

    ; build the value to insert
    INSBL   t1,t0,t3                    t3 = 000a 0000

    ; mask out the values to be replaced
    MSKBL   t5,t0,t5                    t5 = yyy0 xxxx

    ; "or" the new values into place
    BIS     t5,t3,t5                    t5 = yyya xxxx

    ; and write the results back out
    STQ_U   t5,(t0)
</pre>
<p>Dealing with unaligned memory on the Alpha AXP is very annoying. Notice that updates to words and bytes, even aligned words, is not atomic. We read the entire quad from memory, perform some register calculations, and then write the entire quad back out. If somebody made a change to another byte within the quad, we will wipe out that change when we complete our word or byte update. </p>
<p>Next time, we’ll look at atomic memory operations. </p>
<p><b>Bonus chatter</b>: There is one more pair of instructions which operate on the bytes within a register: <code>ZAP</code> and <code>ZAPNOT</code>. </p>
<pre>
    ZAP     Ra, Rb/#b, Rc  ; Rc = Ra after zeroing the bytes selected by Rb/#b
    ZAPNOT  Ra, Rb/#b, Rc  ; Rc = Ra after zeroing the bytes selected by ~Rb/#b
</pre>
<p>The <code>ZAP</code> and <code>ZAPNOT</code> instructions treat the low-order 8 bits of the second parameter as references to the corresponding bytes of the <var>Ra</var> register: Bit <var>n</var> of <var>Rb</var>/#b corresponds to bits <var>N</var> × 8 through <var>N</var> × 8 + 7. The <code>ZAP</code> instruction sets the byte to zero if the corresponding bit is set; the <code>ZAPNOT</code> instruction sets the byte to zero if the corresponding bit is clear. The other 56 bits of the second parameter are ignored. </p>
<p>For example, <code>ZAP v0, #128, v0</code> clears the top byte of <var>v0</var>, and <code>ZAPNOT v0, #128, v0</code> clears all but the top byte of <var>v0</var>. (For some reason, I had trouble remembering which way is which. My trick was to pretend that the <code>ZAPNOT</code> instruction is called <code>KEEP</code>.) </p>
<p>As a special case, these instructions provide a handy way to zero-extend a register. </p>
<pre>
    ZAPNOT  Ra, #1, Rc  ; zero-extend byte from Ra to Rc
    ZAPNOT  Ra, #3, Rc  ; zero-extend word from Ra to Rc
    ZAPNOT  Ra, #15, Rc ; zero-extend long from Ra to Rc
</pre>
<p>Note that in the last case, zero-extending a negative long will result in a 32-bit value in non-canonical form. But you hopefully were expecting that; if you want to sign-extend the value (in order to ensure a value in canonical form), you would have done <code>ADDL Ra, #0, Rc</code>. </p>


</body>